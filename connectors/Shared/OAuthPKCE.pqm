// PKCE OAuth requirements:
// 1. Create a "Desktop, Mobile, Single-Page App" APS application
// 2. Register the following Callback URL: "https://oauth.powerbi.com/views/oauthredirect.html"
// 3. Create a _config.json_ with { "APS_CLIENT_ID": "your client id" }

let
    AUTH_BASE_URL = "https://developer.api.autodesk.com/authentication/v2/",
    REDIRECT_URI = "https://oauth.powerbi.com/views/oauthredirect.html",
    CONFIG = Json.Document(Extension.Contents("config.json")),
    APS_CLIENT_ID = if CONFIG[APS_CLIENT_ID]? = null then error "Missing APS_CLIENT_ID" else  CONFIG[APS_CLIENT_ID],
    SCOPES = "data:read",
    WINDOW_WIDTH = 600,
    WINDOW_HEIGHT = 600,

    Base64Encode = (s) => Text.Replace(Text.Replace(Text.BeforeDelimiter(Binary.ToText(s, BinaryEncoding.Base64), "="), "+", "-"), "/", "_"),

    StartLogin = (dataSourcePath, state, display) =>
        let
            codeVerifier = Text.NewGuid() & Text.NewGuid(),
            codeChallenge = Base64Encode(Crypto.CreateHash(CryptoAlgorithm.SHA256, Text.ToBinary(codeVerifier))),
            query = [
                response_type = "code",
                client_id = APS_CLIENT_ID,
                redirect_uri = REDIRECT_URI,
                scope = SCOPES,
                code_challenge_method = "S256",
                code_challenge = codeChallenge
            ]
        in
            [
                LoginUri = Uri.Combine(AUTH_BASE_URL, "authorize") & "?" & Uri.BuildQueryString(query),
                CallbackUri = REDIRECT_URI,
                Context = codeVerifier,
                WindowWidth = WINDOW_WIDTH,
                WindowHeight = WINDOW_HEIGHT
            ],

    FinishLogin = (context, callbackUri, state) =>
        let
            parts = Uri.Parts(callbackUri)[Query]
        in
            TokenMethod(parts[code], "authorization_code", context),

    TokenMethod = (code, grantType, optional context) =>
        let
            codeVerifier = if context <> null then [code_verifier = context] else [],
            codeParameter = if grantType = "authorization_code" then [code = code] else [refresh_token = code],
            query = [
                client_id = APS_CLIENT_ID,
                grant_type = grantType,
                redirect_uri = REDIRECT_URI
            ],
            queryString = Uri.BuildQueryString(codeVerifier & codeParameter & query),
            response = Web.Contents(
                Uri.Combine(AUTH_BASE_URL, "token"),
                [
                    Content = Text.ToBinary(queryString),
                    Headers = [
                        #"Content-Type" = "application/x-www-form-urlencoded",
                        #"Accept" = "application/json"
                    ]
                ]
            ),
            result = Json.Document(response)
        in
            if (result[error]? <> null) then
                error Error.Record(result[error], result[message]?)
            else
                result,

    RefreshToken = (dataSourcePath, refreshToken) => TokenMethod(refreshToken, "refresh_token")
in
    [
        StartLogin = StartLogin,
        FinishLogin = FinishLogin,
        RefreshToken = RefreshToken
    ]