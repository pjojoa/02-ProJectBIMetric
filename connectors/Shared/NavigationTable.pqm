let
    FromTable = (_table as table, keyColumns as list, nameColumn as text, dataColumn as text, itemKindColumn as text, itemNameColumn as text, isLeafColumn as text) as table =>
        let
            tableType = Value.Type(_table),
            newTableType = Type.AddTableKey(tableType, keyColumns, true) meta 
            [
                NavigationTable.NameColumn = nameColumn, 
                NavigationTable.DataColumn = dataColumn,
                NavigationTable.ItemKindColumn = itemKindColumn, 
                Preview.DelayColumn = itemNameColumn, 
                NavigationTable.IsLeafColumn = isLeafColumn
            ],
            navigationTable = Value.ReplaceType(_table, newTableType)
        in
            navigationTable,

    FromRecords = (records as list) as table => FromTable(Table.FromRecords(records), {"Key"}, "Name", "Data", "ItemKind", "ItemName", "IsLeaf"),
    RecordType = type [ Name = text, Key = text, Data = any, ItemKind = text, ItemName = text, IsLeaf = logical ],
    ListType = type { RecordType },
    FromRecordsType = type function (records as ListType) as any
in
    [
        FromTable = FromTable,
        FromRecords = Value.ReplaceType(FromRecords, FromRecordsType)
    ]