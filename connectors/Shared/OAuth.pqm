// OAuth requirements:
// 1. Create an APS application
// 2. Register the following Callback URL: "https://oauth.powerbi.com/views/oauthredirect.html"
// 3. Create a _config.json_ with { "APS_CLIENT_ID": "your client id", "APS_CLIENT_SECRET": "your client secret" }

let
    AUTH_BASE_URL = "https://developer.api.autodesk.com/authentication/v2/",
    REDIRECT_URI = "https://oauth.powerbi.com/views/oauthredirect.html",
    CONFIG = Json.Document(Extension.Contents("config.json")),
    APS_CLIENT_ID = if CONFIG[APS_CLIENT_ID]? = null then error "Missing APS_CLIENT_ID" else  CONFIG[APS_CLIENT_ID],
    APS_CLIENT_SECRET = if CONFIG[APS_CLIENT_SECRET]? = null then error "Missing APS_CLIENT_SECRET" else  CONFIG[APS_CLIENT_SECRET],
    SCOPES = "data:read",
    WINDOW_WIDTH = 600,
    WINDOW_HEIGHT = 600,

    StartLogin = (dataSourcePath, state, display) =>
        let
            query = [
                response_type = "code",
                client_id = APS_CLIENT_ID,
                scope = SCOPES,
                redirect_uri = REDIRECT_URI,
                state = state
            ]
        in
            [
                LoginUri = Uri.Combine(AUTH_BASE_URL, "authorize") & "?" & Uri.BuildQueryString(query),
                CallbackUri = REDIRECT_URI,
                Context = null,
                WindowWidth = WINDOW_WIDTH,
                WindowHeight = WINDOW_HEIGHT
            ],

    FinishLogin = (context, callbackUri, state) =>
        let
            parts = Uri.Parts(callbackUri)[Query]
        in
            TokenMethod(parts[code]),

    TokenMethod = (code) =>
        let
            query = [
                grant_type = "authorization_code",
                code = code,
                redirect_uri = REDIRECT_URI
            ],
            response = Web.Contents(
                Uri.Combine(AUTH_BASE_URL, "authorize"),
                [
                    Content = Text.ToBinary(Uri.BuildQueryString(query)),
                    Headers = [
                        #"Content-Type" = "application/x-www-form-urlencoded",
                        #"Accept" = "application/json",
                        #"Authorization" = "Basic " & Binary.ToText(Text.ToBinary(APS_CLIENT_ID & ":" & APS_CLIENT_SECRET), BinaryEncoding.Base64)
                    ]
                ]
            )
        in
            Json.Document(response),

    RefreshToken = (dataSourcePath, refreshToken) =>
        let
            query = [
                grant_type = "refresh_token",
                refresh_token = refreshToken
            ],
            response = Web.Contents(
                Uri.Combine(AUTH_BASE_URL, "token"),
                [
                    Content = Text.ToBinary(Uri.BuildQueryString(query)),
                    Headers = [
                        #"Content-Type" = "application/x-www-form-urlencoded",
                        #"Accept" = "application/json",
                        #"Authorization" = "Basic " & Binary.ToText(Text.ToBinary(APS_CLIENT_ID & ":" & APS_CLIENT_SECRET), BinaryEncoding.Base64)
                    ]
                ]
            )
        in
            Json.Document(response)
in
    [
        StartLogin = StartLogin,
        FinishLogin = FinishLogin,
        RefreshToken = RefreshToken
    ]